ViewXModel:
    tx_import=Import
    // support wildcard * and separator ;
    apply_to=ApplyTo
    views+=ClassView
;

Import:
    'tximport' model=ID 'from' path=PathRegEx
;

PathRegEx:
    /"\.?(\w|\.\.|\/|[-])+(\.tx)?"/
;

ApplyTo:
    'apply' 'to' regex=/"((\w|\*(?!\*))+\.(\w|\*(?!\*))+)(;(\w|\*(?!\*))+\.(\w|\*(?!\*))+)*"/
;

ClassView:
    'view' name=ID 'as' shape=Shape ('inside' parentView=[ClassView])?
    ('{' properties+=ViewProperty '}')?
;

Shape:
    'Graph' | 'State' | 'Circle' | 'Ellipse' | 'Rectangle' | 'Line'
;

ViewProperty:
    ShapeProperty | Label | EdgeProperty
;

ShapeProperty:
    BackgroundProperty | StrokeProperty | BorderProperty
;

BackgroundProperty:
    'background' ':' background=Background
;

Background:
    ColorFn | ImageFn
;

ColorFn:
    'color' '(' color=Color ')'
;

Color:
    'white' | 'black' | 'red' | 'green' | 'blue' | 'yellow' | 'orange' | 'purple' | /#[0-9a-fA-F]{6}/
;

ImageFn:
    'image' '(' image=PathRegEx ')'
;

StrokeProperty:
    'stroke' ':' width=INT color=Color style=StrokeStyle
;

BorderProperty:
    'border' ':' border=Border
;

StrokeStyle:
    'solid' | 'dotted' | 'dashed'
;

Border:
    width=INT style=BorderStyle color=Color
;

BorderStyle:
    'solid' | 'dotted' | 'dashed' | 'double'
;

Label:
    'label' ':' (label=STRING | label=ClassLabel) ('{' labelProperties+=LabelProperty '}')?
;

ClassLabel:
    classView=[ClassView] ('.' classProperties+=ID['.'])?
;

EdgeProperty:
    EdgeStartProperty | EdgeEndProperty
;

EdgeStartProperty:
    'start' ':' classView=[ClassView] ('.' classProperties+=ID['.']) // this class property needs to be defined here as ClassView
    ('{' arrowProperties+=ArrowProperty '}')?
;

EdgeEndProperty:
    'end' ':' classView=[ClassView] ('.' classProperties+=ID['.'])
    ('{' arrowProperties+=ArrowProperty '}')?
;

ArrowProperty:
    'arrow' ':' scale=INT (shape=ArrowShape)? (fill=ArrowFill)? (color=Color)?
;

ArrowShape:
    'tee' | 'triangle' | 'triangle-tee' | 'triangle-cross' | 'triangle-backcurve' | 'square' | 'circle' | 'diamond' | 'none'
;

ArrowFill:
    'filled' | 'hollow'
;

LabelProperty:
    LabelFont | LabelWrap | LabelBackground
    // | LabelBorder | LabelOutline
;

LabelFont:
    'font' ':' size=INT (style=LabelFontStyle)? (color=Color)?
;

LabelWrap:
    'wrap' ':' 
;

LabelBackground:
    'background' ':' color=Color (opacity=INT)?
;

LabelFontStyle:
    'normal' | 'italic' | 'oblique'
;

Comment:
  /\/\/.*$/
;